// tag::plugins
buildscript {
    ext.kotlin_version = "1.3.72"

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}
apply plugin: "kotlin"
// end::plugins

// tag::properties
group "com.petrovich"
version "0.2.0-SNAPSHOT"

final def jackson_version = "2.9.2"
final def junit_version = "5.6.2"
// end::properties

// tag::repositories
repositories {
    mavenCentral()
}
// end::repositories

// tag::jar
jar {
    manifest.attributes(
            "Created-By": "Dmitrii Kniazev",
            "Implementation-Version": archiveVersion,
            "Java-Version": sourceCompatibility.toString(),
            "Build-Time": new Date().toString()
    )
}
// end::jar

// tag::dependencies
dependencies {
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    compile("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jackson_version")
    compile("com.fasterxml.jackson.core:jackson-databind:$jackson_version")
    //test dependencies
    testCompile("org.jetbrains.kotlin:kotlin-test")
    testCompile("org.junit.jupiter:junit-jupiter-api:$junit_version")
    testCompile("org.junit.jupiter:junit-jupiter-params:$junit_version")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:$junit_version")
}
// end::dependencies

// tag::source sets
sourceSets {
    main {
        resources {
            srcDirs += ["petrovich-rules/"]
            includes += ["gender.yml", "rules.yml"]
        }
    }
    test {
        resources {
            srcDirs += ["petrovich-eval/"]
            includes += ["*.tsv", "*.csv"]
        }
    }
}
// end::source sets

// tag::kotlin-compile
compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8.toString()
}
compileTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8.toString()
}
// end::kotlin-compile

test {
    useJUnitPlatform()
}
